plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'me.geek.tom'
version '2.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://files.minecraftforge.net/maven/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    // Discord
    implementation 'com.discord4j:discord4j-core:3.1.0'

    // Logging
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.1'
    implementation 'org.apache.logging.log4j:log4j-iostreams:2.13.1'
    implementation 'io.sentry:sentry:1.7.30'

    // Config
    implementation 'com.electronwill.night-config:core:3.6.3'
    implementation 'com.electronwill.night-config:toml:3.6.3'

    // Command parsing
    implementation 'com.mojang:brigadier:1.0.17'
    implementation 'com.beust:jcommander:1.71'

    // Mappings
    implementation 'net.fabricmc:tiny-mappings-parser:0.3.0+build.17'
    implementation 'net.minecraftforge:srgutils:0.2.12'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.5'

    // I/O
    implementation 'com.github.Steveice10:OpenNBT:master-SNAPSHOT'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.okio:okio:2.8.0'

    // Utils
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'it.unimi.dsi:fastutil:8.2.2'
    implementation 'com.github.zafarkhaja:java-semver:0.9.0'
    implementation 'org.kohsuke:github-api:1.116'
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes([
                "Specification-Title": "McBot",
                "Specification-Vendor": "Tom_The_Geek",
                "Specification-Version": "2", // We are version 2!
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"Tom_The_Geek",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Main-Class": "me.geek.tom.mcbot.McBot"
        ])
    }
}

build.finalizedBy('shadowJar')
